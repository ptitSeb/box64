# Maintainer: Jai-JAP <parjailu@gmail.com>
# Author: Sebastien Chevalier <ptitseb@box86.org>
pkgname=box64-tegra-git
pkgver=1116.9243ea4
pkgrel=1
pkgdesc="Linux Userspace x86_64 Emulator with a twist, targeted at ARM64 Linux devices."
arch=('aarch64')
url="https://github.com/ptitSeb/box64"
license=('MIT')
optdepends=('gl4es: OpenGL 2 for GLES 2 devices')
makedepends=('git' 'cmake' 'make')
provides=("${pkgname%-git}" "${pkgname%-tegra-git}-git" "${pkgname%-tegra-git}")
conflicts=("${pkgname%-git}" "${pkgname%-tegra-git}-git" "${pkgname%-tegra-git}")
source=('git+https://github.com/ptitSeb/box64')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-tegra-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${pkgname%-tegra-git}"
	if [[ ! -d ./build ]]; then
		mkdir build && cd build
		cmake .. -DTEGRAX1=1 -DCMAKE_BUILD_TYPE=Debug
	fi
}

build() {
	cd "$srcdir/${pkgname%-tegra-git}/build"
	make -j$(nproc)
}

package() {
	cd "$srcdir/${pkgname%-tegra-git}/build"
	make DESTDIR="${pkgdir}/" install
	# /usr/local/bin isn't in PATH by the default,
	# we should move it to /usr/bin
	cd ${pkgdir}
	mv usr/local/bin/ usr/bin/
	# cleanup when dir is empty
	rmdir usr/local || exit 0
}
