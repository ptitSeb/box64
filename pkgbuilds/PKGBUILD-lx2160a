# Maintainer: Jai-JAP <jai.jap.318@gmail.com>
# Author: Sebastien Chevalier <ptitseb@box86.org>
pkgname=box64-lx2160a-git
pkgver=r2431.60c6858
pkgrel=1
pkgdesc="Linux Userspace x86_64 Emulator with a twist, targeted at ARM64 Linux devices."
arch=('aarch64')
url="https://github.com/ptitSeb/box64"
license=('MIT')
optdepends=('gl4es: OpenGL 2 for GLES 2 devices')
makedepends=('git' 'cmake' 'make')
provides=("${pkgname%-git}" "${pkgname%-lx2160a-git}-git" "${pkgname%-lx2160a-git}")
conflicts=("${pkgname%-git}" "${pkgname%-lx2160a-git}-git" "${pkgname%-lx2160a-git}")
source=('git+https://github.com/ptitSeb/box64')
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname%-lx2160a-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${pkgname%-lx2160a-git}"
	[[ ! -d ./build ]] && mkdir build
	cmake -B build -DLX2160A=1 -DCMAKE_BUILD_TYPE=RelWithDebInfo -DCMAKE_INSTALL_PREFIX=/usr
}

build() {
	cd "${srcdir}/${pkgname%-lx2160a-git}/build"
	make -j$(nproc)
}

package() {
	cd "${srcdir}/${pkgname%-lx2160a-git}/build"
	make DESTDIR="${pkgdir}/" install
}
